plugins {
	id 'java'
	id 'jacoco-report-aggregation'
	id 'com.github.spotbugs' version '4.8.0' apply true
	id 'maven-publish'
}

group 'org.wuerthner.score'
version '1.3.0-SNAPSHOT'

repositories {
	mavenLocal()
	mavenCentral()
	maven {
		url "https://plugins.gradle.org/m2/"
	}
}

spotbugs {
	ignoreFailures = true
	excludeFilter = file("spotbugsExclude.xml")
	reportLevel = "high"
}

tasks.named('check') {
	dependsOn tasks.named('testCodeCoverageReport', JacocoReport)
}

publishing {
	repositories {
		maven {
			name = "GitHubPackages"
			url = uri("https://maven.pkg.github.com/jwuert/score")
			credentials {
				username = System.getenv("GITHUB_ACTOR")  
				password = System.getenv("GITHUB_TOKEN")
			}
		}
	}
	publications {
		maven(MavenPublication) {
			artifactId = rootProject.name
			from components.java
		}
	}
}

dependencies {
	testImplementation group: 'junit', name: 'junit', version: '4.13.2'
}
